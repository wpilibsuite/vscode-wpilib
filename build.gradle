plugins {
    id 'edu.wpi.first.wpilib.repositories.WPILibRepositoriesPlugin' version '2020.2'
    id 'edu.wpi.first.wpilib.versioning.WPILibVersioningPlugin' version '4.0.2'
    id "de.undercouch.download" version "4.0.1"
    id "base"
}

if (project.hasProperty('buildServer')) {
    wpilibVersioning.buildServerMode = true
}

ext.addedRelease = false
wpilibVersioning.useAllTags = true

def azureRelease = System.getenv('BUILD_SOURCEBRANCH')
if (azureRelease != null) {
    println azureRelease
    if (azureRelease.startsWith('refs/tags/')) {
        wpilibVersioning.releaseMode = true
        wpilibRepositories.addAllReleaseRepositories(project)
        ext.addedRelease = true
        println 'tagged build'
    }
}

if (!ext.addedRelease) {
    wpilibRepositories.addAllDevelopmentRepositories(project)
}

configurations {
    cppSource
    javaSource
}

dependencies {
    cppSource 'edu.wpi.first.wpilibc:examples:+:@zip'
    javaSource 'edu.wpi.first.wpilibj:examples:+:@zip'

    cppSource 'edu.wpi.first.wpilibc:templates:+:@zip'
    javaSource 'edu.wpi.first.wpilibj:templates:+:@zip'

    cppSource 'edu.wpi.first.wpilibc:commands:+:@zip'
    javaSource 'edu.wpi.first.wpilibj:commands:+:@zip'
}


task extractCppDependencies(type: Copy) {
    dependsOn configurations.cppSource

    from {
        configurations.cppSource.collect { it }
        configurations.cppSource.collect { zipTree(it) }
    }
    into 'vscode-wpilib/resources/cpp/src'
}

task extractJavaDependencies(type: Copy) {
    dependsOn configurations.javaSource

    from {
        configurations.javaSource.collect { zipTree(it) }
    }
    into 'vscode-wpilib/resources/java/src'
}

def downloadNewCommands = tasks.register('downloadNewCommands', Download) {
  src 'https://raw.githubusercontent.com/ThadHouse/allwpilib/commandjson/wpilibNewCommands/WPILibNewCommands.json'
  dest 'vscode-wpilib/resources/vendordeps/WPILibNewCommands.json'
  overwrite true
}

def downloadOldCommands = tasks.register('downloadOldCommands', Download) {
  src 'https://raw.githubusercontent.com/ThadHouse/allwpilib/commandjson/wpilibOldCommands/WPILibOldCommands.json'
  dest 'vscode-wpilib/resources/vendordeps/WPILibOldCommands.json'
  overwrite true
}

task copyFromVsCodeToStandaloneResources(type: Copy) {
    dependsOn extractCppDependencies
    dependsOn extractJavaDependencies
    dependsOn downloadNewCommands
    dependsOn downloadOldCommands
    from ('vscode-wpilib/resources/java') {
        into 'java'
    }
    from ('vscode-wpilib/resources/cpp') {
        into 'cpp'
    }
    from ('vscode-wpilib/resources/gradle') {
        into 'gradle'
    }
    from ('vscode-wpilib/resources/vendordeps') {
        into 'vendordeps'
    }
    from ('vscode-wpilib/resources/eclipseprojectmain.java') {
        into '/'
    }
    into 'wpilib-utility-standalone/resources'
}

task copyFromVsCodeToStandaloneSrc(type: Copy) {
    from ('vscode-wpilib/src/riolog/shared') {
        into 'riolog/shared'
    }
    from ('vscode-wpilib/src/shared') {
        into 'shared'
    }
    into 'wpilib-utility-standalone/src'
}



task updateAllDependencies() {
    dependsOn copyFromVsCodeToStandaloneResources
    dependsOn copyFromVsCodeToStandaloneSrc
}

if (project.hasProperty("publishVersion")) {
    wpilibVersioning.version.set(project.publishVersion)
}

wpilibVersioning.version.finalizeValue()

ext.pubVersion = wpilibVersioning.version.get()


if (pubVersion == '') {
    pubVersion = '0.0.1-unknown'
}

apply from: 'versions.gradle'

wrapper {
    gradleVersion = '6.0'
}
