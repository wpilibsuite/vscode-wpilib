plugins {
    id 'org.wpilib.WPILibRepositoriesPlugin' version '2027.0.0'
    id 'org.wpilib.WPILibVersioningPlugin' version '2027.0.1'
    id "de.undercouch.download" version "5.6.0"
    id "base"
}

if (project.hasProperty('buildServer')) {
    wpilibVersioning.buildServerMode = true
}

ext.addedRelease = false
wpilibVersioning.useAllTags = true
wpilibRepositories.use2027Repos()

def azureRelease = System.getenv('GITHUB_REF')
if (azureRelease != null) {
    println azureRelease
    if (azureRelease.startsWith('refs/tags/')) {
        wpilibVersioning.releaseMode = true
        wpilibRepositories.addAllReleaseRepositories(project)
        ext.addedRelease = true
        println 'tagged build'
    }
}

if (!ext.addedRelease) {
    wpilibRepositories.addAllDevelopmentRepositories(project)
}

configurations {
    cppSource
    javaSource
}

dependencies {
    cppSource 'org.wpilib.wpilibc:examples:+:@zip'
    javaSource 'org.wpilib.wpilibj:examples:+:@zip'

    cppSource 'org.wpilib.wpilibc:templates:+:@zip'
    javaSource 'org.wpilib.wpilibj:templates:+:@zip'

    cppSource 'org.wpilib.wpilibc:commands:+:@zip'
    javaSource 'org.wpilib.wpilibj:commands:+:@zip'
}

task extractCppDependencies(type: Copy) {
    dependsOn configurations.cppSource

    from {
        configurations.cppSource.collect { it }
        configurations.cppSource.collect { zipTree(it) }
    }
    into 'vscode-wpilib/resources/cpp/src'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

task extractJavaDependencies(type: Copy) {
    dependsOn configurations.javaSource

    from {
        configurations.javaSource.collect { zipTree(it) }
    }
    into 'vscode-wpilib/resources/java/src'

    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
}

def downloadCommandsV2 = tasks.register('downloadCommandsV2', Download) {
  src 'https://raw.githubusercontent.com/wpilibsuite/allwpilib/2027/commandsv2/CommandsV2.json'
  dest 'vscode-wpilib/resources/vendordeps/CommandsV2.json'
  overwrite true
}

def downloadCommandsV3 = tasks.register('downloadCommandsV3', Download) {
  src 'https://raw.githubusercontent.com/wpilibsuite/allwpilib/2027/commandsv3/CommandsV3.json'
  dest 'vscode-wpilib/resources/vendordeps/CommandsV3.json'
  overwrite true
}

def downloadRomiVendordep = tasks.register('downloadRomiVendordep', Download) {
  src 'https://raw.githubusercontent.com/wpilibsuite/allwpilib/2027/romiVendordep/RomiVendordep.json'
  dest 'vscode-wpilib/resources/vendordeps/RomiVendordep.json'
  overwrite true
}

def downloadXRPVendordep = tasks.register('downloadXRPVendordep', Download) {
  src 'https://raw.githubusercontent.com/wpilibsuite/allwpilib/2027/xrpVendordep/XRPVendordep.json'
  dest 'vscode-wpilib/resources/vendordeps/XRPVendordep.json'
  overwrite true
}

build.dependsOn extractCppDependencies
build.dependsOn extractJavaDependencies
build.dependsOn downloadCommandsV2
build.dependsOn downloadCommandsV3
build.dependsOn downloadRomiVendordep
build.dependsOn downloadXRPVendordep

apply from: 'templatebuilder.gradle'

if (project.hasProperty("publishVersion")) {
    wpilibVersioning.version.set(project.publishVersion)
}

wpilibVersioning.version.finalizeValue()

ext.pubVersion = wpilibVersioning.version.get()

if (pubVersion == '') {
    pubVersion = '0.0.1-unknown'
}

apply from: 'versions.gradle'

wrapper {
    gradleVersion = '9.2.0'
}
